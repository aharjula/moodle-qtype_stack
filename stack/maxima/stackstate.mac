/*  Author Matti Harjula
    Aalto University
    Copyright (C) 2015 Matti Harjula

    This program is free software: you can redistribute it or modify
    it under the terms of the GNU General Public License version two.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
    GNU General Public License for details.

    You should have received a copy of the GNU General Public License
    along with this program. If not, see <http://www.gnu.org/licenses/>. */


/****************************************************************/
/*  State variable related functions and definitions for STACK  */
/*                                                              */
/*  Matti Harjula <matti.harjula@aalto.fi>                      */
/*  V0.81 March 2016                                            */
/*                                                              */
/****************************************************************/

/* We use structures to store the state variables and the state of them. */
/* The storage of a variable depends greatly on the context (instance or global) */
/* The PHP-side will ignore the variables that have not changed as saving them is pointless */

/* THE LOGIC OF THE CONTEXTS */
/* 'instance' is the context of this question with this seed for this user */
/* 'global' is shared between questions and seeds but belongs to this user */
/* */
/* as the values of the 'global' variables can change due to actions elsewhere */
/* while this 'instance' is being processed all 'global' variables the have */
/* been mentioned in the code get copied to 'instance' variables when the question */
/* is instantiated, and get-commands on 'global' variables first check for 'instance' */
/* variables of the same name as this means that the real values of 'global' */
/* variables may be hidden there are special functions for acting on those values */
/* typically you'll want to act on 'global' counters, sets or lists and for these */
/* task there are specific functions, you may also directly set a 'global' variables */
/* value but note that you do not know what others have done to it elsewhere... */
/* */
/* 'user' is a collection of random read-only details about the user, name */
/*        and student-id and such not really state but a way to access values */
/* 'attempt' will eventually contain and so on */

/* Structure to help keeping track of the various details of the variables */
defstruct (stackstatevar (ctx="global",name="unnamed",value=0,changed=false));

/* And we keep our state vars in a list for later use */
stackstatevars:[];

/* The state variables come from stack using this function */
/* and users should not call this function */
stack_state_load(ctx,name,value) := block([sv],
 sv:new(stackstatevar),
 sv@name:name,
 sv@ctx:ctx,
 sv@value:value,
 stackstatevars:append(stackstatevars,[sv]),
 return(sv)
);

/* Non functional function only used on stack side to declare variables and their use, including whether they are r or rw */
stack_state_declare(rw,ctx,name,initial_value) :=block([sv,v],
 sv:false,
 for v in stackstatevars do if (is(v@name=name) and is(v@ctx=ctx)) then sv:v,
 if is(sv=false) then (sv:stack_state_load(ctx,name,initial_value),sv@changed:true),
 return(false)
);

/* Gets the value of a state variable */
stack_state_get(ctx,name) := block([sv,v],
 /* do we even have that? */
 sv:false,
 if is(ctx="global") then (for v in stackstatevars do if (is(v@name=name) and is(v@ctx="instance")) then sv:v),
 if not is(sv=false) then return(sv@value),
 for v in stackstatevars do if (is(v@name=name) and is(v@ctx=ctx)) then sv:v,
 if is(sv=false) then return(und),
 return(sv@value)
);

/* Sets the value of a state variable, may create a new variable so watch what you do */
stack_state_set(ctx,name,value) := block([sv,v],
 /* do we even have that? */
 sv:false,
 for v in stackstatevars do if (is(v@name=name) and is(v@ctx=ctx)) then sv:v,
 if is(sv=false) then (sv:stack_state_load(ctx,name,value),sv@changed:true),
 if not is(sv@value=value) then sv@changed:true,
 sv@value:value,
 return(value)
);

/* Increment once for an instance, increments a global variable */
/* if it has not been incremented before in this instance handy for progress tracking */
/* The variables context is always 'global' and will always generate additional 'instance' variable */
/* If the variable is undefined will create it and set to 0 */
/* Will fail if the length of the name is close to max length, as this will try to expand it */
stack_state_increment_once(name) := block([sv,v],
 /* get the variable */
 sv:false,
 for v in stackstatevars do if (is(v@name=name) and is(v@ctx="global")) then sv:v,
 if is(sv=false) then (sv:stack_state_load("global",name,0),sv@changed:true),
 /* check the lock */
 v:stack_state_get("instance",sconcat("[i]",name),0),
 if is(v=0) then (sv@value:sv@value+1,sv@changed:true,stack_state_set("instance",sconcat("[i]",name),1))
);

stack_state_decrement_once(name) := block([sv,v],
 /* get the variable */
 sv:false,
 for v in stackstatevars do if (is(v@name=name) and is(v@ctx="global")) then sv:v,
 if is(sv=false) then (sv:stack_state_load("global",name,0),sv@changed:true),
 /* check the lock */
 v:stack_state_get("instance",sconcat("[d]",name),0),
 if is(v=0) then (sv@value:sv@value-1,sv@changed:true,stack_state_set("instance",sconcat("[d]",name),1))
);

stack_state_full_state(showreadonly) :=block([v],
 /* This is the way to access the statevariable store, as using the variable name directly is not an option */
 if is(showreadonly=true) then return(stackstatevars),
 return(sublist(stackstatevars,lambda([v],is(v@ctx="global") or is(v@ctx="instance"))))
);

/* Unwraps the condensed PRT-history to usable variables. Not supposed to be called by the user. */
stack_state_load_prt(prtname) :=block([prti,data,v,c,rawdata,prev],
    /* Die if called in incomplete context */
    if is(stack_state_get("structure","prt-inputs")=und) or is(stack_state_get("structure","prts")=und) then return(false),

    /* Find the index of this PRT. */
    prti:false,
    c:1,
    for v in stack_state_get("structure","prts") do (if is(v=prtname) then prti:c,c:c+1),
    /* The names of the inputs */
    data:stack_state_get("structure","prt-inputs"),
    stack_state_set(sconcat("prt=",prtname),"required-inputs",data[prti]),
    /* The history */
    data:stack_state_get("instance",sconcat("[",prtname,"]")),
    /* There is an edge case when nothing has been added to the data and it cannot be primed */
    if is(data=und) then data:[],
    /* Split from the string form input data we do not need it anymore */
    c:length(stack_state_get(sconcat("prt=",prtname),"required-inputs")),
    rawdata:makelist(first(v),v,data),
    stack_state_set(sconcat("prt=",prtname),"history",rawdata),
    stack_state_set(sconcat("prt=",prtname),"count-attempts",length(rawdata)),
    /* Previous input is the second last element as the current input has already been added */
    if is(length(rawdata)<2) then prev:makelist(und,v,1,c) else prev:second(reverse(rawdata)),
    stack_state_set(sconcat("prt=",prtname),"previous-input",prev),
    c:1,
    for v in stack_state_get(sconcat("prt=",prtname),"required-inputs") do (
        stack_state_set(sconcat("prt=",prtname),sconcat("previous-input-",v),prev[c]),
        c:c+1
    )
);

/* Appends new data to PRT-history, again something the user should not call. */
stack_state_update_prt(prtname,newdata) :=block([data,prev],
    data:stack_state_get("instance",sconcat("[",prtname,"]")),
    if is(data=und) then (
        data:[newdata],
        stack_state_set("instance",sconcat("[",prtname,"]"),data),
        /* Update the PRT-state */
        stack_state_load_prt(prtname)
    ) else (
        /* Only append if the input string has changed */
        prev:last(data),
        if not is(second(prev)=second(newdata)) then (
            data:append(data,[newdata]),
            stack_state_set("instance",sconcat("[",prtname,"]"),data),
            /* Update the PRT-state */
            stack_state_load_prt(prtname)
        )
    )
);

/* Convenience functions, TODO: add type checks for the params... */
stack_state_append(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:append(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);

stack_state_push(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:push(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);

stack_state_rest(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:rest(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);

stack_state_union(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:union(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);

stack_state_intersection(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:intersection(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);

stack_state_setdifference(ctx,name,param) := block([tmp],
    tmp:stack_state_get(ctx,name),
    tmp:setdifference(tmp,param),
    return(stack_state_set(ctx,name,tmp))
);
